// Sources:
// https://www.lysator.liu.se/c/ANSI-C-grammar-y.html
// https://github.com/ranveeraggarwal/simple-c-like-compiler/blob/master/parser/grammar
use lalrpop_util::ParseError;
use std::str::FromStr;
use super::ast::*;
use crate::asm;
grammar;

extern {
    type Error = asm::parser::ParseError;
}

//--------MODULE------------
pub module: Module = {
    function_declaration => {
        Module { functions: vec![<>] }
    },
	<mut m: module> <f: function_declaration> => {
        m.functions.push(f);
        m
    }
};

//--------FUNCTIONS------------
function_declaration: Box<FunctionDeclaration> = {
	<k: type_specifier> <i: identifier> "(" <p: Comma<parameter>?> ")" <s: compound_statement> => Box::new(
        FunctionDeclaration {
            ret: k,
            name: i,
            params: p.unwrap_or_else(|| Vec::new()),
            stmt: s,
            id: 0
        }
    )
};

parameter: Parameter = {
    <k: type_specifier> <i: identifier> => {
        Parameter {
            kind: k,
            name: i,
            id:   0
        }
    }
};

//--------EXPRESSIONS------------
expression_list: Vec<Expr> = {
	expression => vec![*<>],
	<mut v: expression_list> "," <e: expression> => {
        v.push(*e);
        v
    }
};
expression: Box<Expr> = {
    <l: unary_expression> <o: AssignOp> <r: expression> => {
        Box::new(Expr::Binary(
            BinaryExpr {
                binary_op: o,
                left: l,
                right: r,
                kind: None
            }
        ))
    },
    or_expression
};
AssignOp: BinaryOp = {
    "+=" => BinaryOp::Peq,
    "-=" => BinaryOp::Seq,
    "*=" => BinaryOp::Teq,
    "/=" => BinaryOp::Deq,
    "="  => BinaryOp::Assign,
}

or_expression = Tier<OrOp, and_expression>;
OrOp: BinaryOp = {"||" => BinaryOp::Or};

and_expression = Tier<AndOp, eq_expression>;
AndOp: BinaryOp = {"&&" => BinaryOp::And};

eq_expression = Tier<EqOp, rel_expression>;
EqOp: BinaryOp = {
    "==" => BinaryOp::Eq,
    "!=" => BinaryOp::Neq
};

rel_expression = Tier<RelOp, add_expression>;
RelOp: BinaryOp = {
    ">"  => BinaryOp::Gt,
    "<"  => BinaryOp::Lt,
    ">=" => BinaryOp::Geq,
    "<=" => BinaryOp::Leq
};

add_expression = Tier<AddOp, mul_expression>;
AddOp: BinaryOp = {
    "+" => BinaryOp::Add,
    "-" => BinaryOp::Sub
};

mul_expression = Tier<MulOp, unary_expression>;
MulOp: BinaryOp = {
    "*" => BinaryOp::Mul,
    "/" => BinaryOp::Div,
    "%" => BinaryOp::Mod,
};

unary_expression: Box<Expr> = {
    postfix_expression,
    <u: UnaryOp> <p: postfix_expression> => {
        Box::new(Expr::Unary(
            UnaryExpr {
                unary_op: u,
                expr:     p,
                kind:     None
            }
        ))
    }
};
UnaryOp: UnaryOp = {
    "*" => UnaryOp::Star,
    "!" => UnaryOp::Not,
    "-" => UnaryOp::Neg,
    "&" => UnaryOp::Address
};

postfix_expression: Box<Expr> = {
	identifier => Box::new(Expr::Ident(Identifier {
        name: <>, kind: None, id: 0
    })),
    integer => Box::new(Expr::Integer(<>)),
    float => Box::new(Expr::Float(<>)),
    <i: identifier> "[" <e: expression_list> "]" => 
        Box::new(Expr::Access(AccessExpr {
            name:    i,
            offsets: e,
            sizes:   Vec::new(),
            kind:    None,
            id:      0
        }))
    ,
	<i: identifier> "(" <args: expression_list?> ")" => {
        match args {
            None => Box::new(Expr::Function(
                FunctionCall { name: i, args: Vec::new(), kind: None, id: 0 }
            )),
            Some(v) => Box::new(Expr::Function(
                FunctionCall { name: i, args: v, kind: None, id: 0 }
            )),
        }
    },
    "(" <expression> ")"
};

//--------STATEMENTS------------
statement_list: Vec<Statement> = {
	statement => vec![*<>],
	<mut v: statement_list> <s: statement> => {
        v.push(*s);
        v
    }
};

statement: Box<Statement> = {
	compound_statement,
    declare_statement,
	expression_statement,
	selection_statement,
	iteration_statement,
	jump_statement,
    asm_statement
};

asm_statement: Box<Statement> = {
    "asm!" "{" <asm: asm_list> "}" => {
        Box::new(Statement::Asm(
            AsmStatement { asm }
        ))
    }
};
asm_list: Vec<asm::asm::AA> = {
    asm_line => vec![<>],
    <mut v: asm_list> "," <a: asm_line> => {
        v.push(a);
        v
    }
};
asm_line: asm::asm::AA = {
    <s:r#""[a-zA-Z0-9\-,#\[\] ]*""#> =>? 
        asm::parser::parse(s[1..(s.len()-1)].to_string())
        .map_err(|e| ParseError::User {
            error: e
        })
};

compound_statement: Box<Statement> = {
	"{" <s: statement_list?> "}" => {
        match s {
            None => Box::new(Statement::Compound(
                CompoundStatement { stmts: Vec::new()}
            )),
            Some(l) => Box::new(Statement::Compound(
                CompoundStatement { stmts: l}
            ))
        }
    }
};

declare_statement: Box<Statement> = {
    <k: type_specifier> <i: identifier> ";" => {
        Box::new(Statement::Declare(
            DeclareStatement { id: 0, kind: k, name: i, val: None }
        ))
    },
    <k: type_specifier> <i: identifier> "=" <e: expression> ";" => {
        Box::new(Statement::Declare(
            DeclareStatement { id: 0, kind: k, name: i, val: Some(e) }
        ))
    }
};

expression_statement: Box<Statement> = {
	<expression?> ";" => Box::new(Statement::Expr(
        ExprStatement { expr: <> }
    ))
};

// TODO: allow non-bracketed else?
selection_statement: Box<Statement> = {
    "if" "(" <e: expression> ")" <s1: statement> => Box::new(
        Statement::If( IfStatement {
            condition: e,
            true_stmt: s1,
            false_stmt: None
        })
    ),
    "if" "(" <e: expression> ")" <s1: compound_statement>
    "else" <s2: compound_statement>  => Box::new(
        Statement::If( IfStatement {
            condition: e,
            true_stmt: s1,
            false_stmt: Some(s2)
        })
    )
};

iteration_statement: Box<Statement> = {
	"while" "(" <e: expression> ")" <s: statement> => Box::new(
        Statement::While( WhileStatement {
            condition: e,
            stmt: s
        })
    ),
	"for" "(" <i: simple_statement> <e1: expression?> ";" <e2: expression?> ")"
        <s: statement> => Box::new(
        Statement::For( ForStatement { 
            init: i,
            cond: e1,
            each: e2,
            stmt: s
        })
    )
};

simple_statement: Box<Statement> = {
    declare_statement,
    expression_statement
}

jump_statement: Box<Statement> = {
	"continue" ";" => Box::new(Statement::Jump(
        JumpStatement { 
            jump_type: JumpOp::Continue,
            expr: None
        }
    )),
    "break" ";" => Box::new(Statement::Jump(
        JumpStatement { 
            jump_type: JumpOp::Break,
            expr: None
        }
    )),
    "return" <expression?> ";" => Box::new(Statement::Jump(
        JumpStatement { 
            jump_type: JumpOp::Return,
            expr: <>
        }
    ))
};

//--------TYPES------------
type_specifier: Kind = {
    <t: type_primitive> <p: pointer?> => {
        match p {
            None    => Kind { indir: 0, prim: t },
            Some(c) => Kind { indir: c, prim: t }
        }
        
    }
};

type_primitive: Primitive = {
    "void"  => Primitive::Void,
    "int"   => Primitive::Int,
    "float" => Primitive::Float
};

pointer: u32 = {
    "*" => 1,
    <pointer> "*" => <> + 1
};

identifier: String = {
    r"[a-zA-Z][a-zA-Z0-9_]*" => <>.to_string()
};

integer: i64 = {
    r"\-?[0-9]*" => i64::from_str(<>).unwrap()
};

float: f64 = {
    r"\-?[0-9]+\.[0-9]*" => f64::from_str(<>).unwrap()
};

//--------MACROS------------
Comma<T>: Vec<T> = { // (1)
    <mut v:(<T> ",")*> <e:T> => { // (2)
        v.push(e);
        v
    }
};

Tier<Op, NextTier>: Box<Expr> = {
    <l: Tier<Op, NextTier>> <o: Op> <r: NextTier> => {
        Box::new(Expr::Binary(
            BinaryExpr {
                binary_op: o,
                left: l,
                right: r,
                kind: None
            }
        ))
    },
    NextTier
};

match {
    r"\s*" => { }, // The default whitespace skipping is disabled if an `ignore pattern` is specified
    r"//[^\n\r]*[\n\r]*" => { }, // Skip `// comments`
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => { },  // Skip `/* comments */`
} else {
    _
}